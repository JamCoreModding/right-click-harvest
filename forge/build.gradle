plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "me.shedaniel.unified-publishing"
}

architectury {
    platformSetupLoomIde()
    forge()
}

base {
    archivesName = "rightclickharvest-forge-${minecraft_version}"
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

        mixinConfig "rightclickharvest.mixins.json"
    }
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge libs.forge
    modApi libs.architectury.forge
    modImplementation libs.jamlib.forge

    forgeRuntimeLibrary "blue.endless:jankson:1.2.3"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }
}

if (System.getenv("CURSEFORGE_API_KEY") != null && System.getenv("MODRINTH_API_KEY") != null) {
    unifiedPublishing {
        project {
            displayName = "V${project.version} (Forge ${project.minecraft_version})"
            changelog = rootProject.file("CHANGELOG.md").text
            releaseType = project.version.contains("beta") ? "beta" : "release"
            gameVersions = [project.minecraft_version]
            gameLoaders = ["forge"]

            mainPublication tasks.jar

            relations {
                depends {
                    curseforge = "architectury-api"
                    modrinth = "architectury-api"
                }

                depends {
                    curseforge = "jamlib"
                    modrinth = "jamlib"
                }
            }

            curseforge {
                token = System.getenv("CURSEFORGE_API_KEY")
                id = "452834"
                gameLoaders.add("NeoForge")
            }

            modrinth {
                token = System.getenv("MODRINTH_API_KEY")
                id = "Cnejf5xM"
                gameLoaders.add("neoforge")
            }
        }
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archivesBaseName + "-forge"
            from components.java
        }
    }

    repositories {
        if (System.getenv("MAVEN_USERNAME") != null && System.getenv("MAVEN_PASSWORD") != null) {
            maven {
                url = "https://maven.jamalam.tech/releases"
                credentials {
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_PASSWORD")
                }
            }
        }
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
